project.plugins.apply('maven-publish')
project.plugins.apply('signing')

def localProperties=new Properties()
localProperties.load(project.file('local.properties').newDataInputStream())

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'io.github.wslaimin'
                artifactId = 'RVAdapter'
                version = '1.0.0'

                from components.release
                // generate pom.xml
                pom {
                    name = 'RVAdapter'
                    description = 'Simply defining adapters of RecyclerView in Android'
                    url = 'https://github.com/wslaimin/RVAdapter.git'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'wslaimin'
                            name = 'TFly'
                            email = 'wslaimin@gmail.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/wslaimin/RVAdapter.git'
                    }
                }
            }
        }

        repositories {
            maven {
                // change to point to your repo
                def releaseRepo = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotRepo = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'

                url = publishing.publications.release.version.endsWith('SNAPSHOT') ? snapshotRepo : releaseRepo
                credentials {
                    username = localProperties['credencial.username']
                    password = localProperties['credencial.password']
                }
            }
            maven {
                name 'Build'
                url = layout.buildDirectory.dir('repo')
            }
        }
    }

    if(localProperties['signing.keyId']&&localProperties['signing.password']&&localProperties['signing.secretKeyRingFile']){
        signing {
            sign publishing.publications.release
        }
    }
}
